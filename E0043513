      ******************************************************************
      *                                                                *
      *          I D E N T I F I C A T I O N   D I V I S I O N         *
      *                                                                *
      ******************************************************************
       IDENTIFICATION                  DIVISION.
          PROGRAM-ID.                  E0043513.
          AUTHOR                       FOURSYS.
      ******************************************************************
      *                                                                *
      *          E N V I R O N M E N T         D I V I S I O N         *
      *                                                                *
      ******************************************************************
       ENVIRONMENT                     DIVISION.

      *-----------------------*
      * CONFIGURATION SECTION *
      *----------------------------------------------------------------*
       CONFIGURATION                   SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------*
      * INPUT OUTPUT SECTION *
      *----------------------------------------------------------------*
       INPUT-OUTPUT                    SECTION.

       FILE-CONTROL.

           SELECT FILE008 ASSIGN TO FILE008
                  FILE STATUS IS WRK-FS-FILE008.

           SELECT REL009 ASSIGN TO REL009
                  FILE STATUS IS WRK-FS-REL009.

      ******************************************************************
      *                                                                *
      *          D A T A                       D I V I S I O N         *
      *                                                                *
      ******************************************************************
       DATA                            DIVISION.
      *
      *---------------*
      * FILE SECTION  *
      *----------------------------------------------------------------*
       FILE SECTION.

       FD  FILE008
           RECORDING MODE IS F.
      *    LABEL RECORD IS STANDARD
      *    BLOCK CONTAINS 12 RECORDS
      *    DATA RECORD IS FD-FILE008-REGISTRO.
       01  REG-FILE008.
           COPY B135F008.

       FD  REL009
           RECORDING MODE IS F.
      *    LABEL RECORD IS STANDARD
      *    BLOCK CONTAINS 12 RECORDS
      *    DATA RECORD IS FD-REL009-REGISTRO.
       01  REG-REL009                      PIC X(157).

      *-------------------------*
      * WORKING STORAGE SECTION *
      *----------------------------------------------------------------*
       WORKING-STORAGE                 SECTION.

      * FLAGS
       01  WRK-FLAGS-SWITCHES.
           05  WRK-DEBUG               PIC  X(01)  VALUE 'N'.
               88  WRK-DEBUG-NAO                   VALUE 'N'.
               88  WRK-DEBUG-SIM                   VALUE 'S'.

      * FILE STATUS
       01  WRK-STATUS.

           05  WRK-FS-FILE008          PIC  X(02)  VALUE SPACES.
               88  WRK-FS-FILE008-OK               VALUE '00'.
               88  WRK-FS-FILE008-EOF              VALUE '10'.

           05  WRK-FS-REL009           PIC  X(02)  VALUE SPACES.
               88  WRK-FS-REL009-OK                VALUE '00'.
               88  WRK-FS-REL009-EOF               VALUE '10'.

      ******************************************************************
      *                LAYOUT DO REL009RIO                             *
      ******************************************************************
      *LINHA DE DETALHE
      ******************************************************************
       01  WS-LINDET.

           05 LD-CPF                       PIC X(11).
           05 FILLER                       PIC X(03)
                                                   VALUE
              ' | '.
           05 LD-NOME                      PIC X(100).
           05 FILLER                       PIC X(03)
                                                   VALUE
              ' | '.
           05 LD-CARGO                     PIC X(04).
           05 FILLER                       PIC X(04)
                                                   VALUE
              ' | R'.
           05 LD-SALARIO-BASE              PIC $ZZ.ZZZ.ZZ9,99.
           05 FILLER                       PIC X(04)
                                                   VALUE
              ' | R'.
           05 LD-VALOR-HORA-EXTRA          PIC $ZZ.ZZZ.ZZ9,99.
           05 FILLER                       PIC X(09)
                                                   VALUE SPACES.
      * COPYBOOKS
       01  WRK-BKALERRO.
           COPY BKALERRO.
      ******************************************************************
      *                                                                *
      *          P R O C E D U R E             D I V I S I O N         *
      *                                                                *
      ******************************************************************
       PROCEDURE                       DIVISION.
      *----------------------------------------------------------------*
      * 0000-PRINCIPAL: ESTRUTURA PRINCIPAL DO PROGRAMA.               *
      *----------------------------------------------------------------*
       0000-MAIN                       SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '0000-MAINLINE'
           END-IF

           PERFORM 1000-INICIAR
              THRU 1000-INICIAR-FIM

           PERFORM 2000-PROCESSAR
              THRU 2000-PROCESSAR-FIM
             UNTIL WRK-FS-FILE008-EOF

           PERFORM 3000-FINALIZAR
              THRU 3000-FINALIZAR-FIM

           STOP RUN.

       0000-FIM.                       EXIT.
      *----------------------------------------------------------------*
      * 1000-INICAR: TODOS OS PROCEDIMENTOS PARA INICIAR O PROGRAMA DA *
      *    FORMA CORRETA COMO: ABRIR ARQUIVOS                          *
      *----------------------------------------------------------------*
       1000-INICIAR                    SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 1000-INICIAR'
           END-IF

           PERFORM 1100-ABRIR-ARQUIVOS
              THRU 1100-ABRIR-ARQUIVOS-FIM

           PERFORM 9100-LER-FILE008
              THRU 9100-LER-FILE008-FIM.

       1000-INICIAR-FIM.               EXIT.
      *----------------------------------------------------------------*
      * 1100-ABRIR-ARQUIVOS: REALIZA ABERTURA DOS ARQUIVOS A SEREM UTI-*
      *    LIZADOS NO PROGRAMA REALIZANDO OS TRATAMENTOS EM CASO DE    *
      *    ERRO                                                        *
      *----------------------------------------------------------------*
       1100-ABRIR-ARQUIVOS             SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '  1100-ABRIR-ARQUIVOS'
           END-IF

           OPEN INPUT FILE008

           IF  NOT  WRK-FS-FILE008-OK

               MOVE 'FILE008'          TO BKALERRO-ARQ-NOME
               MOVE 'ABERTURA'         TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE008     TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM
           END-IF

           OPEN OUTPUT REL009

           IF  NOT  WRK-FS-REL009-OK

               MOVE 'REL009'           TO BKALERRO-ARQ-NOME
               MOVE 'ABERTURA'         TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-REL009      TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF.
       1100-ABRIR-ARQUIVOS-FIM.        EXIT.
      *----------------------------------------------------------------*
      * 2000-PROCESSAR TODA A REGRA DE NEGOCIO DEVE ESTAR PRESENTE NES-*
      *    SE PARAGRAFO                                                *
      *----------------------------------------------------------------*
       2000-PROCESSAR                  SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 2000-PROCESSAR'
           END-IF

           PERFORM 9100-IMPRIME-DETALHE
              THRU 9100-IMPRIME-DETALHE-FIM.

           PERFORM 9100-LER-FILE008
              THRU 9100-LER-FILE008-FIM.

       2000-PROCESSAR-FIM.             EXIT.
      *----------------------------------------------------------------*
      * 9100- PARAGRAFO DIRECIONADO PARA GRAVAR O ARQUIVO FILE005      *
      *----------------------------------------------------------------*
       9100-IMPRIME-DETALHE              SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 9100-IMPRIME-DETALHE'
           END-IF

           MOVE B135F008-CPF          TO LD-CPF
           MOVE B135F008-NOME         TO LD-NOME
           MOVE B135F008-ID-CARGO     TO LD-CARGO
           MOVE B135F008-SALARIO-BASE TO LD-SALARIO-BASE
           MOVE B135F008-VALOR-HORA-EXTRA
                                       TO LD-VALOR-HORA-EXTRA

           WRITE REG-REL009 FROM WS-LINDET
           IF NOT WRK-FS-REL009-OK
                MOVE 'REL009'           TO BKALERRO-ARQ-NOME
                MOVE 'GRAVACAO'         TO BKALERRO-ARQ-MOMENTO
                MOVE WRK-FS-REL009      TO BKALERRO-ARQ-FILESTATUS

                PERFORM 9999-ABEND
                    THRU 9999-ABEND-FIM
              END-IF.

       9100-IMPRIME-DETALHE-FIM.       EXIT.
      *----------------------------------------------------------------*
      * 3000-FINALIZAR: SAO REALIZADOS TODOS OS PROCEDIMENTOS PARA FI- *
      *    NALIZAR O PROGRAMA DA FORMA CORRETA. OS ARQUIVOS SAO FECHA- *
      *    DOS E A ESTATISTICA DO PROGRAMA EH REALIZADA                *
      *----------------------------------------------------------------*
       3000-FINALIZAR                  SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 3000-FINALIZAR'
           END-IF

           PERFORM 3100-FECHAR-ARQUIVOS

              THRU 3100-FECHAR-ARQUIVOS-FIM.

       3000-FINALIZAR-FIM.             EXIT.
      *----------------------------------------------------------------*
      * 3100-FECHAR ARQUIVOS: TODOS OS ARQUIVOS DEVEM SER FECHADOS COM *
      *    OS RESPECTIVOS TRATAMENTOS DE FILE STATUS REALIZADOS        *
      *----------------------------------------------------------------*
       3100-FECHAR-ARQUIVOS            SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '  3100-FECHAR-ARQUIVOS'
           END-IF

           CLOSE FILE008

           IF  NOT  WRK-FS-FILE008-OK

               MOVE '3100-FECHAR-ARQUIVOS'
                                       TO BKALERRO-PARAGRAFO
               MOVE 'FILE008'          TO BKALERRO-ARQ-NOME
               MOVE 'FECHAMENTO'       TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE008     TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF

           CLOSE REL009

           IF  NOT  WRK-FS-REL009-OK

               MOVE '3100-FECHAR-ARQUIVOS'
                                       TO BKALERRO-PARAGRAFO
               MOVE 'REL009'           TO BKALERRO-ARQ-NOME
               MOVE 'FECHAMENTO'       TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-REL009      TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF.

       3100-FECHAR-ARQUIVOS-FIM.       EXIT.
      *----------------------------------------------------------------*
      * 9100-LER-ARQUIVO                                               *
      * DESC: TODOS OS PROCEDIMENTOS PARA INICIAR O PROGRAMA DA FORMA  *
      *       CORRETA COMO: ABRIR ARQUIVOS                             *
      *----------------------------------------------------------------*
       9100-LER-FILE008                SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 9100-LER-FILE008'
           END-IF

           READ FILE008

           IF  NOT WRK-FS-FILE008-OK
           AND NOT WRK-FS-FILE008-EOF

               MOVE 'FILE008'          TO BKALERRO-ARQ-NOME
               MOVE 'LEITURA'          TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE008     TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF.

       9100-LER-FILE008-FIM.           EXIT.
      *----------------------------------------------------------------*
      * 9999-ABEND                                                     *
      * DESC: UMA VEZ QUE O DIMENTOS PARA FINALIZAR O PROGRAMA DA FOR- *
      *       MA CORRETA COMO: FECHAR ARQUIVOS                         *
      *----------------------------------------------------------------*
       9999-ABEND                      SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '9999-ABEND'
           END-IF

           DISPLAY '***************************************'
           DISPLAY '*                ERRO                 *'
           DISPLAY '***************************************'
           DISPLAY 'PARAGRAFO  : ' BKALERRO-PARAGRAFO
           DISPLAY 'ARQUIVO    : ' BKALERRO-ARQ-NOME
           DISPLAY 'ACAO       : ' BKALERRO-ARQ-MOMENTO
           DISPLAY 'CODIGO     : ' BKALERRO-ARQ-MENSAGEM
           DISPLAY 'FILE STATUS: ' BKALERRO-ARQ-FILESTATUS
           DISPLAY '***************************************'

           MOVE +16             TO WRK-FS-FILE008
           STOP RUN.

       9999-ABEND-FIM.                 EXIT.
