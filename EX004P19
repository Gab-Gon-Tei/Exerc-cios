      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         EX004P16.
       AUTHOR.                             GABRIEL.
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT MOVTOEST ASSIGN          TO MOVTOEST
           FILE STATUS IS FS-MOVTOEST
           .
           SELECT RELMOV01 ASSIGN          TO RELMOV01
           FILE STATUS IS FS-RELMOV01
           .
           SELECT CADPROD ASSIGN           TO CADPROD
           FILE STATUS IS FS-CADPROD
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  MOVTOEST
           RECORDING MODE IS F
           .
       01  REG-MOVTOEST                    PIC X(33).

       FD  RELMOV01
           RECORDING MODE IS F
           .
       01  REG-RELMOV01                    PIC X(80).

       FD  CADPROD
           RECORDING MODE IS F
           .
       01  REG-CADPROD                     PIC X(35).
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
      *    CABECALHO DO RELATORIO
      *----------------------------------------------------------------*
       01  WS-CABEC1.
           05 WS-DATA-CABEC1               PIC X(10).
           05 FILLER                       PIC X(17)
                                                   VALUE SPACES.

           05 FILLER                       PIC X(27)
                                                    VALUE
              "** BOX COMPANY DO BRASIL **".
           05 FILLER                       PIC X(18)
                                                   VALUE SPACES.
           05 WS-HORA-CABEC1               PIC X(08).


       01  WS-CABEC2.
           05 FILLER                       PIC X(40)
                                                   VALUE
              "RELATORIO DE MOVIMENTACAO DE ESTOQUE A P".
           05 FILLER                       PIC X(09)
                                                   VALUE
              "ARTIR DE ".
           05 WS-REF1-CABEC2               PIC 99.
           05 FILLER                       PIC X(01)
                                                   VALUE "/".
           05 WS-REF2-CABEC2               PIC 99.
           05 FILLER                       PIC X(01)
                                                   VALUE "/".
           05 WS-REF3-CABEC2               PIC 9(04).

           05 FILLER                       PIC X(11)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(05)
                                                   VALUE
               "PAG. ".
           05 WS-PAG-CABEC2                PIC Z.ZZ9.
       01  WS-CABEC3.
           05 FILLER                       PIC X(80)
                                                   VALUE ALL "-".
       01  WS-CABEC4.
           05 FILLER                       PIC X(40)
                                                   VALUE                                    
              "  NUMERO      DATA          HORA        ".
           05 FILLER                       PIC X(40)
                                                   VALUE                                 
              " PRODUTO              QUANTIDADE        ".
       01  WS-CABEC5.
           05 FILLER                       PIC X(40)
                                                   VALUE                                   
              "  -----------  ----------   --------  --".
           05 FILLER                       PIC X(40)
                                                   VALUE
              "------------------    ------------------".
      *----------------------------------------------------------------*
      *                          DETALHE                               *
      *----------------------------------------------------------------*
       01  WS-LINDET.
           05 FILLER                       PIC X(09)
                                                   VALUE SPACES.
           05 LD-NUMMOVTO                  PIC 99.999.999.
           05 FILLER                       PIC X(04)
                                                   VALUE SPACES.
           05 LD-DATAMOVTO                 PIC X(10).
           05 FILLER                       PIC X(07)
                                                   VALUE SPACES.
           05 LD-HORAMOVTO                 PIC X(08).
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           05 LD-CODPRODMOVTO              PIC 9999.
           05 FILLER                       PIC X(03)
                                                   VALUE
              " - ".
           05 LD-DESCPRODMOVTO             PIC X(20).
           05 FILLER                       PIC X(04)
                                                   VALUE SPACES.
           05 LD-QTDMOVTO                  PIC ZZ.ZZZ.ZZ9+.
      *----------------------------------------------------------------*
      *                          RODAPE
      *----------------------------------------------------------------*
       01  WS-RODAPE1                      PIC X(80)
                                                   VALUE ALL "-".
       01  WS-RODAPE2.
           05 FILLER                       PIC X(40)
                                                   VALUE
              "APOS O USO UTILIZE ESTE PAPEL COMO RASCU".
           05 FILLER                       PIC X(40)
                                                   VALUE
              "NHO                  RECICLE SUAS IDEIAS".
      *----------------------------------------------------------------*
      *                 VARIAVEIS ESPELHO                              *
      *----------------------------------------------------------------*
           COPY BMOVTOES.
           COPY VARDATA.
           COPY VARPROSS.
           COPY BCADPROD.
      *----------------------------------------------------------------*
      *                 VARIAVEIS DE TRABALHO                          *
      *----------------------------------------------------------------*
       01  WS-DATA-SYS.
           05 WS-ANO-SYS                   PIC 99.
           05 WS-MES-SYS                   PIC 99.
           05 WS-DIA-SYS                   PIC 99.

       01  WS-HORA-SYS.
           05 WS-HORA-SY                   PIC 99.
           05 WS-MIN-SYS                   PIC 99.
           05 WS-SEG-SYS                   PIC 99.

       01  WS-DATA-FORMATADA.
           05 WS-DIA-FT                    PIC 9(02).
           05 FILLER                       PIC X(01)
                                                   VALUE "/".
           05 WS-MES-FT                    PIC 9(02).
           05 FILLER                       PIC X(01)
                                                   VALUE "/".
           05 WS-ANO-FT                    PIC 99.

       01  WS-HORA-FORMATADA.
           05 WS-HORA-FT                   PIC 9(02).
           05 FILLER                       PIC X(01)
                                                   VALUE ":".
           05 WS-MIN-FT                    PIC 9(02).
           05 FILLER                       PIC X(01)
                                                   VALUE ":".
           05 WS-SEG-FT                    PIC 9(02).

      *VARIAVEIS DE FILE STATUS
       01  FS-MOVTOEST                     PIC X(02).
           88 SUCESSO-M                    VALUE '00'.
           88 FIM-ARQUIVO-M                VALUE '10'.
       01  FS-RELMOV01                     PIC X(02).
           88 SUCESSO-R                    VALUE '00'.
           88 FIM-ARQUIVO-R                VALUE '10'.
       01  FS-CADPROD                      PIC X(02).
           88 SUCESSO-C                    VALUE '00'.
           88 FIM-ARQUIVO-C                VALUE '10'.
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
      *VARIAVEIS DE CONTAGEM
       77  WS-CTLIDO                       PIC 9(05).
       77  WS-CTIMPRESSO                   PIC 9(05).
       77  WS-CTDPZ                        PIC 9(05).
      *VARIAVEIS DE CONTROLE DO RELATORIO
       77  WS-CTLIN                        PIC 9(02).
       77  WS-CTPAG                        PIC 9(02).
       77  WS-PULA                         PIC 9(02).

      *VARIAVEL AUXILIAR DE FORMATACAO DATA
       01  WS-ANO-MES-REF-CAD-AUX.
           05 WS-ANO-REF-CAD               PIC 9(04).
           05 WS-MES-REF-CAD               PIC 9(02).
           05 WS-DIA-REF-CAD               PIC 9(02).
      *----------------------------------------------------------------*
      *VARIAVEIS DE CALL
      *----------------------------------------------------------------*
       01  WS-PROGRAMA                     PIC X(08)
                                                   VALUE "PGMAUX03".
       01  WS-MSG-COMUNICACAO.
           05 WS-MENS-CODPROD              PIC 9(04).
           05 WS-MENS-TIPOMOVTO            PIC X(01).
           05 WS-MENS-QTDE                 PIC 9(08) COMP-3.
           05 WS-MENS-CLASSIF              PIC X(01).
      *----------------------------------------------------------------*
       01  WS-TAB-CADPROD.
           05 WS-REG-TAB-CADPROD OCCURS 100 TIMES
              ASCENDING KEY WS-TAB-CODPROD INDEXED BY IDX1.
              10 WS-TAB-CODPROD            PIC 9(04).
              10 WS-TAB-DESCPROD           PIC X(20).
       77  WS-INDICE                       PIC 9(03).
       77  WS-FLAG                         PIC X(01).
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  CAMPO-PARAMETRO.
           05 LS-LENGTH                    PIC S9(04) COMP.
           05 LS-DATAREF                   PIC X(10).
      *----------------------------------------------------------------*
       PROCEDURE DIVISION USING CAMPO-PARAMETRO.
      *----------------------------------------------------------------*
       0000-EX004P16.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
               UNTIL WS-TIPO-REG-MOVTO = 'T'
           PERFORM 3000-TERMINO
           GOBACK
           .
      *
       1000-INICIALIZAR.
           ACCEPT WS-HORA-SYS              FROM TIME
           ACCEPT WS-HORARIO-INICIAL       FROM TIME
           ACCEPT WS-DATA-SYS              FROM DATE

           MOVE ZEROS                      TO WS-CTLIDO
                                              WS-CTPAG
                                              WS-CTIMPRESSO
                                              WS-PULA
                                              WS-CTDPZ 
           MOVE 99                         TO WS-CTLIN

           OPEN INPUT MOVTOEST
           IF NOT SUCESSO-M
              MOVE "ERRO NA ABERTURA DE MOVTOEST" TO WS-MSG
              MOVE FS-MOVTOEST             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           PERFORM 1100-LER-MOVTOEST
           IF FIM-ARQUIVO-M
              MOVE "ARQUIVO DE ENTRADA VAZIO" TO WS-MSG
              MOVE FS-MOVTOEST             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
           IF WS-TIPO-REG-MOVTO NOT = "H"
              MOVE "ARQUIVO SEM HEADER"    TO WS-MSG
              MOVE FS-MOVTOEST             TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *MOVENDO A DATA DO PARAMETRO PARA A REFERENCIA
      *REGUA            1234567890
      *EXEMPLO DE DATA: 2025/04/25     
           MOVE LS-DATAREF(1:4)            TO WS-ANO-REF-CAD
           MOVE LS-DATAREF(6:2)            TO WS-MES-REF-CAD
           MOVE LS-DATAREF(9:2)            TO WS-DIA-REF-CAD

           OPEN OUTPUT RELMOV01
           IF NOT SUCESSO-R
              MOVE "ERRO NA ABERTURA DE RELMOV01" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           PERFORM 1100-LER-MOVTOEST
           IF WS-TIPO-REG-MOVTO NOT = "D"
              MOVE "ARQUIVO SEM DATA"      TO WS-MSG
              MOVE FS-MOVTOEST             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           
           OPEN INPUT CADPROD           
           IF NOT SUCESSO-C
              MOVE "ERRO NA ABERTURA DE CADPROD" TO WS-MSG
              MOVE FS-CADPROD              TO WS-FS
              GO TO 9000-ERRO
           END-IF

           INITIALIZE WS-INDICE
           PERFORM 1500-CARREGA-TABELA
                   UNTIL FIM-ARQUIVO-C
           PERFORM UNTIL WS-INDICE = 100
              ADD 1           TO WS-INDICE
              MOVE 9999       TO WS-TAB-CODPROD(WS-INDICE) 
              MOVE SPACES     TO WS-TAB-DESCPROD(WS-INDICE)
           END-PERFORM 
           .
       1100-LER-MOVTOEST.
           READ MOVTOEST                   INTO WS-REG-MOVTOEST
           IF SUCESSO-M
              ADD 1                        TO WS-CTLIDO
      *     ELSE
      *        IF FIM-ARQUIVO-M
      *           IF WS-CTLIDO > 0
      *           MOVE "ARQUIVO NAO TEM TRAILER" TO WS-MSG
      *           MOVE FS-MOVTOEST               TO WS-FS
      *           GO TO 9000-ERRO
      *           END-IF
           ELSE
                 MOVE "ERRO DE LEITURA MOVTOEST"       TO WS-MSG
                 MOVE FS-MOVTOEST             TO WS-FS
                 GO TO 9000-ERRO
      *        END-IF
           END-IF
           .
       1500-CARREGA-TABELA.
           READ CADPROD                    INTO WS-REG-CADPROD
           IF SUCESSO-C
              ADD 1                        TO WS-INDICE
              MOVE WS-CODPROD              TO WS-TAB-CODPROD(WS-INDICE)
              MOVE WS-DESCPROD             TO WS-TAB-DESCPROD(WS-INDICE)
           ELSE 
              IF NOT FIM-ARQUIVO-C
                 MOVE "ERRO DE LEITURA CADPROD" TO WS-MSG
                 MOVE FS-CADPROD           TO WS-FS
                 GO TO 9000-ERRO
              END-IF
           END-IF
           . 
       2000-PROCESSAR.
           MOVE "N"                        TO WS-FLAG
           IF WS-DATAMOVTO >= LS-DATAREF
              MOVE "S"                     TO WS-FLAG
           END-IF

           IF WS-FLAG = "S"
              MOVE WS-CODPRODMOVTO         TO WS-MENS-CODPROD
              MOVE WS-TIPOMOVTO            TO WS-MENS-TIPOMOVTO
              MOVE WS-QTDMOVTO             TO WS-MENS-QTDE
              CALL WS-PROGRAMA             USING WS-MSG-COMUNICACAO
              CANCEL WS-PROGRAMA
           END-IF
           
           IF WS-MENS-CLASSIF = 0 
              MOVE "N" TO WS-FLAG
           END-IF

           IF WS-FLAG = "S"              
              IF WS-CTLIN > 49
              PERFORM 2100-IMPRIMIR-CABECALHO
              END-IF

              PERFORM 2200-IMPRIMIR-DETALHE

              IF WS-CTLIN = 48
              PERFORM 2300-IMPRIMIR-RODAPE
              END-IF
           ELSE 
              ADD 1                        TO WS-CTDPZ
           END-IF

           PERFORM 1100-LER-MOVTOEST
           .
       2100-IMPRIMIR-CABECALHO.
           MOVE WS-DIA-SYS                 TO WS-DIA-FT
           MOVE WS-MES-SYS                 TO WS-MES-FT
           MOVE WS-ANO-SYS                 TO WS-ANO-FT
           MOVE WS-HORA-SY                 TO WS-HORA-FT
           MOVE WS-MIN-SYS                 TO WS-MIN-FT
           MOVE WS-SEG-SYS                 TO WS-SEG-FT
           MOVE WS-DATA-FORMATADA          TO WS-DATA-CABEC1
           MOVE WS-HORA-FORMATADA          TO WS-HORA-CABEC1
           MOVE WS-ANO-REF-CAD             TO WS-REF1-CABEC2
           MOVE WS-MES-REF-CAD             TO WS-REF2-CABEC2
           ADD 1                           TO WS-CTPAG
           MOVE WS-CTPAG                   TO WS-PAG-CABEC2

           WRITE REG-RELMOV01              FROM WS-CABEC1 AFTER PAGE
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR CABECALHO1" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           WRITE REG-RELMOV01                  FROM WS-CABEC2
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR CABECALHO2" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           WRITE REG-RELMOV01                  FROM WS-CABEC3
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR CABECALHO3" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           WRITE REG-RELMOV01                  FROM WS-CABEC4
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR CABECALHO4" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           WRITE REG-RELMOV01                  FROM WS-CABEC5
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR CABECALHO5" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           MOVE 5                          TO WS-CTLIN
           .
       2200-IMPRIMIR-DETALHE.
           MOVE WS-NUMMOVTO                TO LD-NUMMOVTO
           MOVE WS-DATAMOVTO(7:2)          TO LD-DATAMOVTO(1:2)
           MOVE '/'                        TO LD-DATAMOVTO(3:1)
           MOVE WS-DATAMOVTO(5:2)          TO LD-DATAMOVTO(4:2)
           MOVE '/'                        TO LD-DATAMOVTO(6:2)
           MOVE WS-DATAMOVTO(1:4)          TO LD-DATAMOVTO(7:4)

           MOVE WS-HORAMOVTO(1:2)          TO LD-HORAMOVTO(1:2)
           MOVE ':'                        TO LD-HORAMOVTO(3:1)
           MOVE WS-HORAMOVTO(3:2)          TO LD-HORAMOVTO(4:2)
           MOVE ':'                        TO LD-HORAMOVTO(6:1)
           MOVE WS-HORAMOVTO(5:2)          TO LD-HORAMOVTO(7:2)
           MOVE WS-CODPRODMOVTO            TO LD-CODPRODMOVTO

           IF WS-TIPOMOVTO = 'E'
              MOVE WS-QTDMOVTO             TO LD-QTDMOVTO
           ELSE
              MULTIPLY WS-QTDMOVTO BY -1 GIVING LD-QTDMOVTO
           END-IF

           SEARCH ALL WS-REG-TAB-CADPROD
              AT END 
                 MOVE "NAO LOCALIZADO" TO LD-DESCPRODMOVTO
              WHEN WS-TAB-CODPROD(IDX1) = WS-CODPRODMOVTO
                   MOVE WS-TAB-DESCPROD(IDX1) TO LD-DESCPRODMOVTO
           END-SEARCH

           WRITE REG-RELMOV01                  FROM WS-LINDET
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR DETALHE" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           ADD 1                           TO WS-CTLIN
           ADD 1                           TO WS-CTIMPRESSO
           .
       2300-IMPRIMIR-RODAPE.
           COMPUTE WS-PULA = WS-CTLIN - 49
           WRITE REG-RELMOV01       FROM WS-RODAPE1 AFTER WS-PULA LINES
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR RODAPE1" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           WRITE REG-RELMOV01         FROM WS-RODAPE2
           IF NOT SUCESSO-R
              MOVE "ERRO AO GRAVAR RODAPE2" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           MOVE 50                         TO WS-CTLIN
           .
       3000-TERMINO.
           PERFORM 9000-IMPRIME-DATA

           IF WS-CTLIN < 50
              PERFORM 2300-IMPRIMIR-RODAPE
           END-IF

           PERFORM 4000-FECHAMENTO-ARQUIVOS
           IF WS-QTDREG-MOVTOEST NOT EQUAL WS-CTIMPRESSO
              MOVE 12                      TO RETURN-CODE
              STOP RUN
           END-IF

           ACCEPT WS-HORARIO-FINAL    FROM TIME
           PERFORM 9000-TEMPO-DE-PROCESSAMENTO
           PERFORM 5000-EXIBIR-RESULTADOS
           .
       5000-EXIBIR-RESULTADOS.
           DISPLAY "==================================================="
           DISPLAY "            BOX COMPANY DO BRASIL                  "
           DISPLAY "==================================================="
           DISPLAY " TOTAL DE MOVIMENTOS LIDOS.......: " WS-CTLIDO
           DISPLAY " TOTAL DE PAGINAS IMPRESSAS......: " WS-CTPAG
           DISPLAY " TOTAL DE MOVIMENTOS IMPRESSOS...: " WS-CTIMPRESSO
           DISPLAY " TOTAL DE CONTADORES DESPREZADOS.: " WS-CTDPZ
           DISPLAY "==================================================="
           DISPLAY " TEMPO TOTAL DE PROCESSAMENTO  : "
                                               WS-TEMPO-PROCESSAMENTO-F
           DISPLAY "==================================================="                                                "
           DISPLAY "                                                   "
           DISPLAY "                                                   "
           DISPLAY "                        *             *            "
           DISPLAY "           *                                       "
           DISPLAY "          *                                        "
           DISPLAY "                         *                         "
           DISPLAY "   *        *                                      "
           DISPLAY "                                        *     *    "
           DISPLAY "                                *                  "
           DISPLAY "                 _     _                           "
           DISPLAY "   *            /_|   |_\                *         "
           DISPLAY "           *   //||   ||\\     *                   "
           DISPLAY "              // ||   || \\                        "
           DISPLAY "             //  ||___||  \\        *              "
           DISPLAY "  *    *    /     |   |     \    _           *     "
           DISPLAY "           /    __|   |__    \  /_\                "
           DISPLAY "          / .--~  |   |  ~--. \|   |               "
           DISPLAY "         /.~ __\  |   |  /   ~.|   |    *          "
           DISPLAY "        .~  `=='\ |   | /   _.-'.  |               "
           DISPLAY "       /  /      \|   |/ .-~    _.-'               "
           DISPLAY "      |           +---+  \  _.-~  |                "
           DISPLAY "  *   `=----.____/  #  \____.----='         *      "
           DISPLAY "       [::::::::|  (_)  |::::::::]                 "
           DISPLAY "      .=----~~~~~\     /~~~~~----=.            *   "
           DISPLAY "      |          /`---'\          |      *         "
           DISPLAY "       \  \     /       \     /  /                 "
           DISPLAY "        `.     /         \     .'                  "
           DISPLAY "          `.  /._________.\  .'          *    *    "
           DISPLAY "         *  `--._________.--'                      "
           DISPLAY "   *                                               "
           DISPLAY "        -NEVER TELL ME THE ODDS- *        *        "
           DISPLAY "    *                   *                          "
           DISPLAY "                                                   "
           DISPLAY "             *         *             *             "
           DISPLAY "  *                                                "
           .
       4000-FECHAMENTO-ARQUIVOS.
      *FECHAMENTO DO MOVTOEST
           CLOSE MOVTOEST
           IF FS-MOVTOEST NOT = '00'
              MOVE "ERRO NO FECHAMENTO DE MOVTOEST" TO WS-MSG
              MOVE FS-MOVTOEST             TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *FECHAMENTO DO RELMOV01
           CLOSE RELMOV01
           IF FS-RELMOV01 NOT = '00'
              MOVE "ERRO NO FECHAMENTO RELMOV01" TO WS-MSG
              MOVE FS-RELMOV01             TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *FECHAMENTO DO CADPROD
           CLOSE CADPROD
           IF FS-CADPROD NOT = '00'
              MOVE "ERRO NO FECHAMENTO CADPROD" TO WS-MSG
              MOVE FS-CADPROD             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           .
      *----------------------------------------------------------------*
           COPY ROTERRO.
           COPY ROTPROSS.
           COPY ROTDATA.
