      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         RELADALS.
       AUTHOR.                             FIS-COBOL-ESTAG.
      *----------------------------------------------------------------*
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

       INPUT-OUTPUT                        SECTION.
       FILE-CONTROL.
           SELECT DALSRELAT ASSIGN          TO DALSRELAT
           FILE STATUS IS FS-DALSRELAT
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  DALSRELAT
           RECORDING MODE IS F.
       01  REG-DALSRELAT                    PIC X(80).

      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
      *                 VARIAVEIS ESPELHO                              *
      *----------------------------------------------------------------*
      * BOOK DAS VARIAVEIS DE CALCULO DE TEMPO
           COPY VARDATA.
           COPY VARPROSS.
      *BOOK DAS VARIAVEIS HOSPEDEIRAS
           EXEC SQL
              INCLUDE CLIENTES
           END-EXEC.
      *BOOK DAS VARIAVEIS PARA USO DO SQL
           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.
      * INDICADORES DA TABELA 
       01  WS-INDICADORES.
           05 IUFCLI                       PIC S9(4) COMP.
           05 IDATANASCCLI                 PIC S9(4) COMP.
           05 ICONTACLI                    PIC S9(4) COMP.
           05 IAGENCIACLI                  PIC S9(4) COMP.
      *----------------------------------------------------------------*
      *                 VARIAVEIS DE TRABALHO                          *
      *----------------------------------------------------------------*
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(05) COMP.
           05 WS-CTIMP                     PIC 9(05) COMP.
           05 WS-CTPAG                     PIC 9(05) COMP.
           05 WS-CTLINHA                   PIC 9(05) COMP.
           05 WS-PULA                      PIC 9(05) COMP.

       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZ.ZZ9.
           05 WS-CTIMP-F                   PIC ZZ.ZZ9.
           05 WS-CTPAG-F                   PIC ZZ.ZZ9.

      *VARIAVEIS DE FILE STATUS
       01  FS-DALSRELAT                     PIC X(02).
           88 SUCESSO-DALSRELAT             VALUE '00'.
      *    88 FIM-ARQ-DALSRELAT             VALUE '10'.

       77  WS-SQLCODE                      PIC +9(09).
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
      *----------------------------------------------------------------*
      * LAYOUT DO RELATORIO DALSRELAT
      *----------------------------------------------------------------*
      *                     CABECALHO
      *----------------------------------------------------------------*
       01  WS-CABEC1.
           03 FILLER                       PIC X(01)
                                                   VALUE SPACES.
           03 WS-DATA-CABEC1               PIC X(10).
           03 FILLER                       PIC X(01)
                                                   VALUE SPACES.
           03 WS-HORA-CABEC1               PIC X(08).
           03 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           03 FILLER                       PIC X(32)
                                                   VALUE
              "RELACAO DE CLIENTES-TESTE DALSON".
           03 FILLER                       PIC X(16)
                                                   VALUE SPACES.
           03 FILLER                       PIC X(05)
                                                   VALUE "PAG. ".
           03 WS-PAG-CABEC1                PIC ZZ9.

       01  WS-CABEC2                       PIC X(80)
                                                    VALUE ALL "-".
       01  WS-CABEC3.
           03 FILLER                       PIC X(40)
                                                   VALUE
      *                 1         2         3         4
      *        1234567890123456789012345678901234567890
              "CPF          NOME                       ".
           03 FILLER                       PIC X(40)
                                                   VALUE
      *                 1         2         3         4
      *        1234567890123456789012345678901234567890
              "     UF  DATANASC.   CONTA    AGENCIA   ".

       01  WS-CABEC4.
           03 FILLER                       PIC X(40)
                                                   VALUE
      *                 1         2         3         4
      *        1234567890123456789012345678901234567890
              "-----------  ---------------------------".
           03 FILLER                       PIC X(40)
                                                   VALUE
      *                 1         2         3         4
      *        1234567890123456789012345678901234567890
              "---  --  ----------  -------  ----------".
      *----------------------------------------------------------------*
      *               LINHA DE DETALHE
      *----------------------------------------------------------------*
       01  WS-LINDET.
           03 LD-CPF                       PIC X(11).
           03 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           03 LD-NOME                      PIC X(30).
           03 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           03 LD-UF                        PIC X(02).
           03 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           03 LD-DATANASC                  PIC X(10).
           03 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           03 LD-CONTA                     PIC X(07).
           03 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           03 LD-AGENCIA                   PIC X(09).
      *----------------------------------------------------------------*
      *               RODAPE
      *----------------------------------------------------------------*
       01  WS-RODAPE1                      PIC X(80)
                                                    VALUE ALL "-".
       01  WS-RODAPE2.
           03 FILLER                       PIC X(40)
                                                   VALUE
      *                 1         2         3         4
      *        1234567890123456789012345678901234567890
              "--O RELATORIO EXTRA NAO EXISTE E NAO POD".
           03 FILLER                       PIC X(40)
                                                   VALUE
      *                 1         2         3         4
      *        1234567890123456789012345678901234567890
              "E DE ATACAR. --- O RELATORIO EXTRA------".
      *----------------------------------------------------------------*
      * DATA E HORA PARA RELATORIO
      *----------------------------------------------------------------*
       01 WS-DATA-SYS.
          03 WS-ANO                        PIC 9(02).
          03 WS-MES                        PIC 9(02).
          03 WS-DIA                        PIC 9(02).

       01 WS-DATA-FORMATADA.
          03 WS-DIA                        PIC 9(02).
          03 FILLER                        PIC X(01)
                                                    VALUE "/".
          03 WS-MES                        PIC 9(02).
          03 FILLER                        PIC X(01)
                                                   VALUE "/".
          03 WS-ANO                        PIC 9(02).

       01 WS-HORA-SYS.
          03 WS-HORA                       PIC 9(02).
          03 WS-MINUTO                     PIC 9(02).
          03 WS-SEGUNDO                    PIC 9(02).

       01 WS-HORA-FORMATADA.
          03 WS-HORA                       PIC 9(02).
          03 FILLER                        PIC X(01)
                                                   VALUE ":".
          03 WS-MINUTO                     PIC 9(02).
          03 FILLER                        PIC X(01)
                                                   VALUE ":".
          03 WS-SEGUNDO                    PIC 9(02).

      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
      *0000-EX004P24.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
               UNTIL SQLCODE = +100
           PERFORM 3000-TERMINO
           GOBACK
           .

       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL       FROM TIME
           INITIALIZE WS-CONTADORES-COMP
           MOVE 99                         TO WS-CTLINHA

      *LEITURA DA DATA E HORA PARA O CABECALHO DO RELATORIO
           ACCEPT WS-DATA-SYS FROM DATE
           ACCEPT WS-HORA-SYS FROM TIME
           MOVE CORRESPONDING WS-DATA-SYS  TO WS-DATA-FORMATADA
           MOVE CORRESPONDING WS-HORA-SYS  TO WS-HORA-FORMATADA
      
      *DECLARACAO DO CURSOR PARA SELECIONAR CLIENTES SEM EMAIL
           EXEC SQL
               DECLARE CLIENTESC CURSOR FOR
                   SELECT CPFCLI
                         ,NOMECLI
                         ,UFCLI
                         ,DATANASCCLI
                         ,CONTACLI
                         ,AGENCIACLI
                   FROM CLIENTES
           END-EXEC

      * ABERTURA DO CURSOR
           EXEC SQL
                OPEN CLIENTESC
           END-EXEC

           IF SQLCODE NOT EQUAL 0
              MOVE "ERRO AO FECHAR CURSOR"
                                           TO WS-MSG
              MOVE SQLCODE                 TO WS-SQLCODE
              GO TO 9000-ERRO-DB2
           END-IF

      * ABERTURA DO ARQUIVO DE RELATORIO
           OPEN OUTPUT DALSRELAT
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO ABERTURA DALSRELAT" TO WS-MSG
              MOVE FS-DALSRELAT             TO WS-FS
              GO TO 9000-ERRO
           END-IF

      * LEITURA DA PRIMEIRA LINHA DO CURSOR
           PERFORM 1500-LER-CLIENTESC
           .
       1500-LER-CLIENTESC.
           EXEC SQL
               FETCH CLIENTESC
               INTO :CPFCLI
                    ,:NOMECLI
                    ,:UFCLI           :IUFCLI
                    ,:DATANASCCLI     :IDATANASCCLI
                    ,:CONTACLI        :ICONTACLI
                    ,:AGENCIACLI      :IAGENCIACLI
           END-EXEC

           IF SQLCODE = 0
              ADD 1 TO WS-CTLIDO
              PERFORM 1600-TRATA-INDICATOR
           ELSE
              IF SQLCODE NOT = +100
              MOVE "ERRO LEITURA CURSOR CLIENTESC"
                                           TO WS-MSG
              MOVE SQLCODE                 TO WS-SQLCODE
              GO TO 9000-ERRO-DB2
              END-IF
           END-IF
           .

       1600-TRATA-INDICATOR.
           IF IUFCLI < 0
              MOVE ALL "*"                 TO UFCLI
           END-IF

           IF IDATANASCCLI < 0
              MOVE ALL "*"                 TO DATANASCCLI
           END-IF

           IF ICONTACLI < 0
              MOVE ZEROS                   TO CONTACLI
           END-IF

           IF IAGENCIACLI < 0
              MOVE ZEROS                   TO AGENCIACLI
           END-IF
           .

       2000-PROCESSAR.
           IF WS-CTLINHA > 59
              PERFORM 2100-IMPRIME-CABECALHO
           END-IF

           PERFORM 2200-IMPRIME-DETALHE

           IF WS-CTLINHA = 58
              PERFORM 2300-IMPRIME-RODAPE
           END-IF
           PERFORM 1500-LER-CLIENTESC
           .

       2100-IMPRIME-CABECALHO.
           MOVE WS-DATA-FORMATADA          TO WS-DATA-CABEC1
           MOVE WS-HORA-FORMATADA          TO WS-HORA-CABEC1
           ADD 1 TO WS-CTPAG
           MOVE WS-CTPAG                   TO WS-PAG-CABEC1

           WRITE REG-DALSRELAT FROM WS-CABEC1 AFTER PAGE
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO IMPRESSAO CABECALHO 1"
                                           TO WS-MSG
               MOVE FS-DALSRELAT           TO WS-FS
               PERFORM 9000-ERRO
           END-IF
           WRITE REG-DALSRELAT FROM WS-CABEC2
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO IMPRESSAO CABECALHO 2"
                                           TO WS-MSG
               MOVE FS-DALSRELAT           TO WS-FS
               PERFORM 9000-ERRO
           END-IF
           WRITE REG-DALSRELAT FROM WS-CABEC3
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO IMPRESSAO CABECALHO 3"
                                           TO WS-MSG
               MOVE FS-DALSRELAT           TO WS-FS
               PERFORM 9000-ERRO
           END-IF
           WRITE REG-DALSRELAT FROM WS-CABEC4
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO IMPRESSAO CABECALHO 4"
                                           TO WS-MSG
               MOVE FS-DALSRELAT           TO WS-FS
               PERFORM 9000-ERRO
           END-IF
           MOVE 4                          TO WS-CTLINHA
           .

       2200-IMPRIME-DETALHE.
           MOVE CPFCLI                     TO LD-CPF
           MOVE NOMECLI-TEXT               TO LD-NOME
           MOVE UFCLI                      TO LD-UF
           MOVE DATANASCCLI                TO LD-DATANASC
           MOVE CONTACLI                   TO LD-CONTA
           MOVE AGENCIACLI                 TO LD-AGENCIA

           WRITE REG-DALSRELAT FROM WS-LINDET
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO NA IMPRESSAO DO DETALHE"
                                           TO WS-MSG
              MOVE FS-DALSRELAT             TO WS-FS
              PERFORM 9000-ERRO
           END-IF
           ADD 1 TO WS-CTLINHA
           ADD 1 TO WS-CTIMP
           .

       2300-IMPRIME-RODAPE.
           COMPUTE WS-PULA = 59 - WS-CTLINHA

           WRITE REG-DALSRELAT FROM WS-RODAPE1 AFTER WS-PULA LINES
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO NA IMPRESSAO DO RODAPE 1"
                                           TO WS-MSG
              MOVE FS-DALSRELAT             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           WRITE REG-DALSRELAT FROM WS-RODAPE2
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO NA IMPRESSAO DO RODAPE2"
                                           TO WS-MSG
              MOVE FS-DALSRELAT             TO WS-FS
              GO TO 9000-ERRO
           END-IF

           MOVE 60                         TO WS-CTLINHA
           .

       3000-TERMINO.
      *VERIFICAR IMPRESSAO RODAPE ULTIMA PAGINA

           IF WS-CTLINHA < 60
              PERFORM 2300-IMPRIME-RODAPE
           END-IF

           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTIMP                   TO WS-CTIMP-F
           MOVE WS-CTPAG                   TO WS-CTPAG-F

           EXEC SQL
               CLOSE CLIENTESC
           END-EXEC
           IF SQLCODE NOT EQUAL 0
              MOVE "ERRO AO FECHAR CURSOR"
                                           TO WS-MSG
              MOVE SQLCODE                 TO WS-SQLCODE
              PERFORM 9000-ERRO-DB2
           END-IF

           CLOSE DALSRELAT
           IF NOT SUCESSO-DALSRELAT
              MOVE "ERRO AO FECHAR DALSRELAT"
                                           TO WS-MSG
              MOVE FS-DALSRELAT             TO WS-FS
              PERFORM 9000-ERRO
           END-IF


           ACCEPT WS-HORARIO-FINAL    FROM TIME
           PERFORM 9000-TEMPO-DE-PROCESSAMENTO

      *     PERFORM 9000-IMPRIME-DATA
           PERFORM 5000-EXIBIR-RESULTADOS
           .
       5000-EXIBIR-RESULTADOS.
           DISPLAY "==================================================="
           DISPLAY "        ESTATISTICA FINAL DE PROCESSAMENTO         "
           DISPLAY "==================================================="
           DISPLAY " QTDE. CLIENTES   LIDOS..............: " WS-CTLIDO-F
           DISPLAY " QTDE. CLIENTES IMPRESSOR............: " WS-CTIMP-F
           DISPLAY " QTDE. PAGINAS IMPRESSAS.............: " WS-CTPAG-F
           DISPLAY "==================================================="
           DISPLAY " TEMPO TOTAL DE PROCESSAMENTO  : "
                                               WS-TEMPO-PROCESSAMENTO-F
           DISPLAY "==================================================="
           DISPLAY "                                                   "
           DISPLAY "                                                   "
           DISPLAY "                        *             *            "
           DISPLAY "           *                                       "
           DISPLAY "          *                                        "
           DISPLAY "                         *                         "
           DISPLAY "   *        *                                      "
           DISPLAY "                                        *     *    "
           DISPLAY "                                *                  "
           DISPLAY "                 _     _                           "
           DISPLAY "   *            /_|   |_\                *         "
           DISPLAY "           *   //||   ||\\     *                   "
           DISPLAY "              // ||   || \\                        "
           DISPLAY "             //  ||___||  \\        *              "
           DISPLAY "  *    *    /     |   |     \    _           *     "
           DISPLAY "           /    __|   |__    \  /_\                "
           DISPLAY "          / .--~  |   |  ~--. \|   |               "
           DISPLAY "         /.~ __\  |   |  /   ~.|   |    *          "
           DISPLAY "        .~  `=='\ |   | /   _.-'.  |               "
           DISPLAY "       /  /      \|   |/ .-~    _.-'               "
           DISPLAY "      |           +---+  \  _.-~  |                "
           DISPLAY "  *   `=----.____/  #  \____.----='         *      "
           DISPLAY "       [::::::::|  (_)  |::::::::]                 "
           DISPLAY "      .=----~~~~~\     /~~~~~----=.            *   "
           DISPLAY "      |          /`---'\          |      *         "
           DISPLAY "       \  \     /       \     /  /                 "
           DISPLAY "        `.     /         \     .'                  "
           DISPLAY "          `.  /._________.\  .'          *    *    "
           DISPLAY "         *  `--._________.--'                      "
           DISPLAY "   *                                               "
           DISPLAY "        -NEVER TELL ME THE ODDS- *        *        "
           DISPLAY "    *                   *                          "
           DISPLAY "                                                   "
           DISPLAY "             *         *             *             "
           DISPLAY "  *                                                "
           .

      *----------------------------------------------------------------*
           COPY ROTERRO.
           COPY ROTERROD.
           COPY ROTPROSS.